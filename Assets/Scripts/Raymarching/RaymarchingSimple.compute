// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> Result;
float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;
Texture2D<float4> _SkyboxTexture;
float4 _DirectionalLight;
SamplerState sampler_SkyboxTexture;
static const float PI = 3.14159265f;
static const float Kd = 0.3f;	// Diffuse coefficient
float2 _PixelOffset;
float3 camera_position;
float3 _Albedo;
static const int NUMBER_OF_STEPS = 1024;
static const float MIN_HIT_DISTANE = 0.001f;	// epsilon
static const float MAX_MARCH_DISTANCE = 1024.0f;
int _NumberOfReflections;
float4 _Sphere;
float3 _Ground;
float3 _GroundScale;
float3 _Box;
float3 _BoxScale;
float3 _Prism;
float2 _PrismSize;
float3 _Torus;
float2 _TorusSize;
bool _SmoothBlend = false;
float _BlendCoefficient = 0.0f;
float3 _Mandelbulb;
float3 _Tetrahedron;

struct Shape
{
	bool invert;
};

struct Ray
{
	float3 origin;
	float3 direction;
	float3 energy;
};

Ray CreateRay(float3 origin, float3 direction)
{
	Ray ray;
	ray.origin = origin;
	ray.direction = direction;
	ray.energy = float3(1.0f, 1.0f, 1.0f);	// The ray starts with full throughput on all color channels, and will diminish with each reflection.
	return ray;
}

Ray CreateCameraRay(float2 uv)
{
	// Transform the camera origin to world space
	float3 origin = mul(_CameraToWorld, float4(0.0f, 0.0f, 0.0f, 0.0f)).xyz;
	// Invert the perspective projection of the view-space position
	float3 direction = mul(_CameraInverseProjection, float4(uv, 0.0f, 1.0f)).xyz;	// Note: uv is 2 dimensional
	// Transform the direction from camera to world space and normalize
	direction = mul(_CameraToWorld, float4(direction, 0.0f)).xyz;
	direction = normalize(direction);

	return CreateRay(origin, direction);
}

struct RayHit
{
	float3 position;
	float distance;
	float3 normal;
	int marchSteps;
	float3 albedo;
	float3 specular;
};

RayHit CreateRayHit()
{
	RayHit hit;
	hit.position = float3(0.0f, 0.0f, 0.0f);
	hit.distance = 1.#INF;
	hit.marchSteps = 0;
	hit.normal = float3(0.0f, 0.0f, 0.0f);
	hit.albedo = float3(0.0f, 0.0f, 0.0f);
	hit.specular = float3(1.0f, 1.0f, 1.0f);
	return hit;
}

float2 mandelbulb(float3 position)
{
	float3 z = position;
	z = _Mandelbulb;
	float dr = 1.0;
	float r = 0.0;
	int iterations = 0;
	int power = 9;

	for (int i = 0; i < 15; i++) {
		iterations = i;
		r = length(z);

		if (r > 2) {
			break;
		}

		// convert to polar coordinates
		float theta = acos(z.z / r);
		float phi = atan2(z.y, z.x);
		dr = pow(r, power - 1.0)*power*dr + 1.0;

		// scale and rotate the point
		float zr = pow(r, power);
		theta = theta * power;
		phi = phi * power;

		// convert back to cartesian coordinates
		z = zr * float3(sin(theta)*cos(phi), sin(phi)*sin(theta), cos(theta));
		z += position;
	}
	float dst = 0.5*log(r)*r / dr;
	return float2(iterations, dst * 1);
}

float SmoothMin(float dst_A, float dst_B, float k)
{
	float h = max(k - abs(dst_A - dst_B), 0) / k;
	return min(dst_A, dst_B) - h * h * h * k * 1 / 6.0;
}

float SDF_sphere(float3 eye)
{
	float3 center = _Sphere.xyz;
	float radius = _Sphere.w;

	//eye.xz = (eye.xz + float2(1.0f, 1.0f)) % 2.0f - float2(1.0f, 1.0f);
	return distance(eye, center) - radius;
}

float SDF_ground_plane(float3 eye)
{
	float3 center = _Ground;
	float3 size = _GroundScale;
	float3 o = abs(eye - center) - size;
	float ud = length(max(o, 0));
	float n = max(max(min(o.x, 0), min(o.y, 0)), min(o.z, 0));
	return ud + n;
}

float SDF_box(float3 eye)
{
	float3 center = _Box;
	float3 size = _BoxScale;
	float3 o = abs(eye - center) - size;
	float ud = length(max(o, 0));
	float n = max(max(min(o.x, 0), min(o.y, 0)), min(o.z, 0));
	return ud + n;
}

float SDF_torus(float3 eye)
{
	float3 center = _Torus;
	float r1 = _TorusSize.x;
	float r2 = _TorusSize.y;
	float2 q = float2(length((eye - center).xz) - r1, eye.y - center.y);
	return length(q) - r2;
}

float SDF_prism(float3 eye)
{
	float3 center = _Prism;
	float2 h = _PrismSize;
	float3 p = eye - center;
	float3 q = abs(p);
	return max(q.z - h.y, max(q.x*0.866025f + p.y*0.5f, -p.y) - h.x*0.5f);
}

float SDF_recursive_tetrahedron(float3 z)
{
	float3 a1 = float3(1, -1, 3);
	float3 a2 = float3(-1, -3, 3);
	float3 a3 = float3(1, -3, 1);
	float3 a4 = float3(-1, -1, 1);
	float3 c;
	int n = 0;
	int Iterations = 3;
	float dist, d;
	while (n < Iterations) {
		c = a1; 
		dist = distance(z, a1);
		d = distance(z, a2); 
		if (d < dist) { c = a2; dist = d; }
		d = distance(z, a3); 
		if (d < dist) { c = a3; dist = d; }
		d = distance(z, a4); 
		if (d < dist) { c = a4; dist = d; }
		z = 2.0f * z - c * (2.0f - 1.0f);
		n++;
	}
	return length(z) * pow(2.0f, float(-n));
}

// eye = ray.origin
float SDF(float3 eye)
{
	float dst_ground = SDF_ground_plane(eye);
	float dst_sphere = SDF_sphere(eye);
	float dst_box = SDF_box(eye);
	float dst_torus = SDF_torus(eye);
	float dst_prism = SDF_prism(eye);

	if (_SmoothBlend)
	{
		return min(dst_ground, SmoothMin(SmoothMin(SmoothMin(dst_sphere, dst_torus, _BlendCoefficient), dst_box, _BlendCoefficient), dst_prism, _BlendCoefficient));
	}
	else 
	{
		return min(dst_ground, min(min(min(dst_sphere, dst_torus), dst_box), dst_prism));
	}
}

float3 EstimateNormal(float3 p) 
{
	float x = SDF(float3(p.x + MIN_HIT_DISTANE, p.y, p.z)) - SDF(float3(p.x - MIN_HIT_DISTANE, p.y, p.z));
	float y = SDF(float3(p.x, p.y + MIN_HIT_DISTANE, p.z)) - SDF(float3(p.x, p.y - MIN_HIT_DISTANE, p.z));
	float z = SDF(float3(p.x, p.y, p.z + MIN_HIT_DISTANE)) - SDF(float3(p.x, p.y, p.z - MIN_HIT_DISTANE));
	return normalize(float3(x, y, z));
}

RayHit March(Ray ray) 
{
	float total_distance_travelled = 0.0f;

	RayHit hit = CreateRayHit();
	hit.albedo = _Albedo;
	hit.position = float3(0.0f, 0.0f, 1.0f);
	hit.position = ray.origin;
	for (int i = 0; i < NUMBER_OF_STEPS; i++)
	{
		float dst = 0.0f;
		int shape = 0;
		float dst_sphere = 0.0f;
		float dst_cube = 0.0f;

		dst = SDF(ray.origin);

		// Ray has hit something
		if (dst <= MIN_HIT_DISTANE)
		{
			float3 pointOnSurface = ray.origin + ray.direction * dst;
			hit.position = pointOnSurface;
			float3 normal = EstimateNormal(pointOnSurface - ray.direction * MIN_HIT_DISTANE);
			hit.normal = normal;
			hit.marchSteps = i;
			hit.albedo = _Albedo;
			hit.specular = float3(0.65f, 0.65f, 0.65f);
			hit.distance = dst;
			break;
		}
		total_distance_travelled += dst;
		if (total_distance_travelled >= MAX_MARCH_DISTANCE)
		{
			break;
		}
		ray.origin += ray.direction * dst;
	}
	return hit;
}

float3 Shade(inout Ray ray, RayHit hit)
{
	// ray hit an object in scene
	if (hit.distance < MIN_HIT_DISTANE)
	{
		// For flat shading, we directly return the color of the hit object
		//return hit.albedo;

		// Diffuse calculation
		return saturate(Kd * dot(hit.normal, _DirectionalLight.xyz) * -1) * _DirectionalLight.w * hit.albedo;
	}
	else
	{
		//return float3(0.0f, 0.0f, 0.0f);

		// In case of no collision, we render the sky box
		// Erase the ray's energy - the sky doesn't reflect anything
		ray.energy = 0.0f;
		float theta = acos(ray.direction.y) / -PI;
		float phi = atan2(ray.direction.x, -ray.direction.z) / -PI * 0.0f;
		return _SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(phi, theta), 0).xyz * 1.0f;
	}
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
	uint width, height;
	Result.GetDimensions(width, height);

	// Transform pixel to [-1,1] range
	float2 uv = float2((id.xy) / float2(width, height) * 2.0f - 1.0f);

	// Get a ray for uvs
	Ray ray = CreateCameraRay(uv);
	float3 result = float3(0.0f, 0.0f, 0.0f);

	// Reflection
	/*
	Raymarch to the hit point, then obtain the color at that point.
	Continue on from the hit point in the direction of the reflected  ray until you reach a new hit point.
	Obtain the color at the new point, then add a portion of it to your original color. Repeat the process.
	*/
	for (int i=0; i< _NumberOfReflections; i++)
	{
		RayHit hit = March(ray);
		result += ray.energy * Shade(ray, hit);
		ray.direction = normalize(reflect(ray.direction, hit.normal));
		ray.origin = hit.position + (ray.direction * 0.01f);
		ray.energy *= hit.specular;
		if (!any(ray.energy))
			break;
	}

	// March the ray
	RayHit hit = March(ray);
	result += ray.energy * Shade(ray, hit);
	Result[id.xy] = float4(result, 1);

	//float rim = hit.marchSteps / 25.0f;
	//Result[id.xy] = lerp(float4(result, 1.0f), 1, 0.0f) * rim;
}
